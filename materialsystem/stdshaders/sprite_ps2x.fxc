//========== Copyright (c) Valve Corporation, All rights reserved. ==========//

// STATIC: "VERTEXCOLOR"		"0..1"
// STATIC: "CONSTANTCOLOR"		"0..1"
// STATIC: "HDRTYPE"			"0..2"
// STATIC: "SRGB"               	"0..1"
// STATIC: "SRGB_OUTPUT_ADAPTER"	"0..1"	[ps20b]

// DYNAMIC: "HDRENABLED"		"0..1"
#include "common_fog_ps_fxc.h"

#include "common_ps_fxc.h"
#include "shader_constant_register_map.h"

const float4 g_Color					: register( c0 );
const float g_HDRColorScale			: register( c1 );

const float4 g_FogParams				: register( PSREG_FOG_PARAMS );
const float4 g_EyePos_SpecExponent		: register( PSREG_EYEPOS_SPEC_EXPONENT );

sampler TexSampler	: register( s0 );

struct PS_INPUT
{
	float2 baseTexCoord				: TEXCOORD0;		// Base texture coordinate
	float4 color					: TEXCOORD2;		// Vertex color (from lighting or unlit)
	
	float4 worldPos_projPosZ		: TEXCOORD7;		// Necessary for pixel fog
};

float4_color_return_type main( PS_INPUT i ) : COLOR
{
#if defined(_X360) && defined( CSTRIKE15 )
	// [mariod] - gamma appropriate sprites without adding an extra SHADER_SRGB_READ combo (SRGB provides same info implicitly)
	#if SRGB
		// fix up properly when SHADER_SRGB_READ is added to this shader
		float4 result, sample = tex2D( TexSampler, i.baseTexCoord );
		sample.rgb = GammaToLinear( sample.rgb );
	#else
		float4 result, sample = tex2D( TexSampler, i.baseTexCoord );
	#endif
#else
	float4 result, sample = tex2D( TexSampler, i.baseTexCoord );
#endif

#if VERTEXCOLOR
	sample *= i.color;
#endif

#if CONSTANTCOLOR
	sample *= g_Color;
#endif
	
#if HDRTYPE && HDRENABLED
	sample.xyz *= g_HDRColorScale;
#endif

	float fogFactor = CalcPixelFogFactor( PIXELFOGTYPE, g_FogParams, g_EyePos_SpecExponent.xyz, i.worldPos_projPosZ.xyz, i.worldPos_projPosZ.w );
#if SRGB
	result = FinalOutput( sample, fogFactor, PIXELFOGTYPE, TONEMAP_SCALE_LINEAR );
#else
	result = FinalOutput( sample, fogFactor, PIXELFOGTYPE, TONEMAP_SCALE_GAMMA );
#endif

	// On Posix, we're being forced through a linear-to-gamma curve but don't want it, so we do the opposite here first
#if SRGB_OUTPUT_ADAPTER
	result = GammaToLinear( result );
#endif

	return result;
}

